buildscript {
    repositories {
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"

if (System.getenv().BUILD_NUMBER)
    version += "." + System.getenv().BUILD_NUMBER
group = "mcp.mobius.waila"
archivesBaseName = "hwyla"

sourceCompatibility = targetCompatibility = "1.8"

minecraft {
    version = "${minecraft_version}-${forge_version}"
    mappings = mcp_mappings
    runDir = "run"
}

repositories {
    maven { url = "https://dvs1.progwml6.com/files/maven" } // JEI
}

dependencies {
    deobfProvided "mezz.jei:jei_${jei_version}"
}

processResources {

    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

// Combine main and API source sets
jar {
    from sourceSets.main.output
    from sourceSets.api.output
}

// API jar
tasks.register("apiJar", Jar) {
    classifier = "api"
    from sourceSets.api.output

    // TODO: when FG bug is fixed, remove allJava from the api jar.
    // https://github.com/MinecraftForge/ForgeGradle/issues/369
    // Gradle should be able to pull them from the -sources jar.
    from sourceSets.api.allJava
}

tasks.register("sourcesJar", Jar) {
    classifier = "sources"
    from sourceSets.main.allJava
    from sourceSets.api.allJava
}

tasks.build.dependsOn apiJar, sourcesJar

tasks.withType(JavaCompile).configureEach { task ->
    task.options.encoding = "UTF-8"
}
